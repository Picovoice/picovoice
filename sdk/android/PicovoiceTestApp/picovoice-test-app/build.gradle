import groovy.json.JsonSlurper

apply plugin: 'com.android.application'

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
    if (project.hasProperty("pvTestingAccessKey")) {
        properties.put("pvTestingAccessKey", project.getProperty("pvTestingAccessKey"))
    }

    if (project.hasProperty("storePassword")) {
        properties.put("storePassword", project.getProperty("storePassword"))
    }
    if (project.hasProperty("storeFile")) {
        properties.put("storeFile", project.getProperty("storeFile"))
    }
    if (project.hasProperty("keyAlias")) {
        properties.put("keyAlias", project.getProperty("keyAlias"))
    }
    if (project.hasProperty("keyPassword")) {
        properties.put("keyPassword", project.getProperty("keyPassword"))
    }
}

android {
    compileSdkVersion defaultTargetSdkVersion

    defaultConfig {
        applicationId "ai.picovoice.picovoice.testapp"
        minSdkVersion 21
        targetSdkVersion defaultTargetSdkVersion
        versionCode 2
        versionName "2.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        resValue 'string', 'pvTestingAccessKey', properties.getProperty("pvTestingAccessKey", "")
    }

    signingConfigs {
        release {
            storePassword properties.getProperty("storePassword")
            storeFile file(properties.getProperty("storeFile", ".dummy.jks"))
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }


    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    def testDataFile = file('../../../../resources/.test/test_data.json')
    def parsedJson = new JsonSlurper().parseText(testDataFile.text)
    def languages = []
    parsedJson.tests.parameters.each { t ->
        languages.add([t.language, t.wakeword, t.context_name, t.audio_file])
    }

    if (System.getProperty("testBuildType", "debug") == "integ") {
        testBuildType("release")
    }

    flavorDimensions "language"
    productFlavors {
        en {
            getIsDefault().set(true)
        }

        languages.each { languageInfo ->
            "${languageInfo.get(0)}" {
                applicationIdSuffix ".${languageInfo.get(0)}"
                ext.wakeword = "${languageInfo.get(1)}"
                ext.contextName = "${languageInfo.get(2)}"
                ext.audioFile = "${languageInfo.get(3)}"
            }
        }

        all { flavor ->
            delete fileTree("$projectDir/src/main/assets") {
                exclude '**/.gitkeep'
            }
            String suffix = (flavor.name != "en") ? "_${flavor.name}" : ""
            task("${flavor.name}CopyPorcupineParams", type: Copy) {
                from("$projectDir/../../../../resources/porcupine/lib/common/")
                include("porcupine_params${suffix}.pv")
                into("$projectDir/src/main/assets/models")
            }
            task("${flavor.name}CopyRhinoParams", type: Copy) {
                from("$projectDir/../../../../resources/rhino/lib/common/")
                include("rhino_params${suffix}.pv")
                into("$projectDir/src/main/assets/models")
            }
            task("${flavor.name}CopyWakeword", type: Copy) {
                description = "Copy ${flavor.name} resources"
                from("$projectDir/../../../../resources/porcupine/resources/keyword_files${suffix}/android")
                include("${flavor.ext.wakeword}_android.ppn")
                into("$projectDir/src/main/assets/wakewords")
                rename { String fileName ->
                    fileName.replace("_android", "").replace(" ", "_")
                }
            }
            task("${flavor.name}CopyContext", type: Copy) {
                description = "Copy ${flavor.name} resources"
                from("$projectDir/../../../../resources/rhino/resources/contexts${suffix}/android")
                include("${flavor.ext.contextName}_android.rhn")
                into("$projectDir/src/main/assets/contexts")
                rename { String fileName ->
                    fileName.replace("_android", "").replace(" ", "_")
                }
            }
            task("${flavor.name}WakewordContextName") {
                resValue 'string', 'pvWakeword', flavor.ext.wakeword
                resValue 'string', 'pvContextName', flavor.ext.contextName
                resValue 'string', 'pvAudioFile', flavor.ext.audioFile
            }
            task("${flavor.name}CopyAudio", type: Copy) {
                description = "Copy ${flavor.name} audio resources"
                from("$projectDir/../../../../resources/audio_samples/")
                include("${flavor.ext.audioFile}")
                into("$projectDir/src/main/assets/audio_samples")
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'ai.picovoice:picovoice-android:3.0.0'

    // Espresso UI Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.microsoft.appcenter:espresso-test-extension:1.4')
    androidTestImplementation('androidx.test.espresso:espresso-intents:3.5.1')
}

afterEvaluate {
    android.productFlavors.all {
        flavor ->
            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}CopyPorcupineParams"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}CopyPorcupineParams"
            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}CopyRhinoParams"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}CopyRhinoParams"

            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}CopyWakeword"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}CopyWakeword"
            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}CopyContext"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}CopyContext"

            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}WakewordContextName"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}WakewordContextName"

            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}CopyAudio"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}CopyAudio"
    }
}
